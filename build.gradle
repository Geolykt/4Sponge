buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

version = '1.16.4-5.0.0'
group = 'de.geolykt.forgeirc'
archivesBaseName = 'forgeirc'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'
}

configurations {
    shade
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.4-35.1.13'

    compile 'org.kitteh.irc:client-lib:7.3.0'

    // We use KICL as the IRC library
    shade("org.kitteh.irc:client-lib:7.3.0") {
        exclude group: 'io.netty' // netty is already used by minecraft
    }
}

shadowJar {
    configurations = [project.configurations.shade]

    relocate 'net.engio', 'bork.engio'
    relocate 'io.leangen', 'bork.leangen'
    relocate 'org.checkerframework', 'bork.checkerframework'
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes([
            "Specification-Title": "MinestomIRC", // Minestom IRC provides most of the software specification ForgeIRC uses
            "Specification-Vendor": "Geolykt",
            "Specification-Version": "5", // Minestom IRC is currently at v5.0.0
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Geolykt",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    filesMatching('**/mods.toml') {
        filter {
            it.replace('${version}', version)
        }
    }
}

tasks.build.dependsOn reobfShadowJar
publish.dependsOn('reobfJar')
shadowJar.dependsOn('classes')
jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}